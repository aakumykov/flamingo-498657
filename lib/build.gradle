plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

project.ext {
    artifactId = 'authsdk'
    groupId = 'com.yandex'

    majorVersionNum = 4
    minorVersionNum = 0
    patchVersionNum = 0

    versionCode = 2

    versionName = majorVersionNum + "." + minorVersionNum + "." + patchVersionNum
}

android {
    namespace groupId + '.' + artifactId
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        def addConstant = { constantName, constantValue ->
            manifestPlaceholders += [(constantName): constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("ACTION_YA_SDK_LOGIN", "com.yandex.auth.action.YA_SDK_LOGIN")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testVariants.all {
        it.mergedFlavor.manifestPlaceholders += [
                YANDEX_CLIENT_ID: "fcdddf83a97843ae80815c1c9247015b",
        ]
    }

    unitTestVariants.all {
        it.mergedFlavor.manifestPlaceholders += [
                YANDEX_CLIENT_ID: "fcdddf83a97843ae80815c1c9247015b",
        ]
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.22"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.browser:browser:1.5.0'
    testImplementation 'com.android.support:support-annotations:28.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.mockito:mockito-core:4.1.0'
    testImplementation 'io.mockk:mockk:1.12.4'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = project.groupId
            artifactId = project.artifactId
            version = versionName
            afterEvaluate {
                from components.release
            }
        }
    }
}